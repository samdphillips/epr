#lang rhombus/static

import:
  lib("racket/port.rkt")
  lib("rhombus/private/amalgam.rkt")!evt
  rhombus/thread
  "connector.rhm" open
  "processor.rhm" open
  "serde.rhm"

export:
  PortSource
  PortSink


class PrintSerializer():
  implements serde.Serializer

  override method serialize(val, out_port :: Port.Output):
    println(val, out_port)
    out_port.flush()


class ReadBytesLineEvt(hand):
  implements evt.Evt
  opaque

  constructor (inp :: Port.Input):
    super(port.#{read-bytes-line-evt}(inp))

  override property handle: hand


class PortSource(~input_port: inp :: Port.Input = Port.Input.current(),
                 ~out: out :: OutputConnect):
  nonfinal
  extends Processor
  field inp_evt = #false

  override property name: #'port_source

  override method process_evt():
   unless inp_evt
   | inp_evt
       := ReadBytesLineEvt(inp)
       .wrap(~tail_call: #true,
             fun (bs :: Bytes):
               on_event(bs.utf8_string()))
   inp_evt

  method decorate(v):
    {#'message: v}

  method on_event(v):
    when v is_a String
    | out.send(decorate(v))
      process()

class PortSink(~output_port: out_port :: Port.Output = Port.Output.current(),
               ~serializer: serializer :: serde.Serializer = PrintSerializer(),
               ~in: in :: InputConnect):
  nonfinal
  extends Processor

  override property name: #'port_sink

  override method process_evt():
    in.recv_evt().wrap(~tail_call: #true, fun(v): on_event(v))

  method on_event(val):
    serializer.serialize(val, out_port)
    process()
